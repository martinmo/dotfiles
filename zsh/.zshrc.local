## use the vi navigation keys (hjkl) besides cursor keys in menu completion
bindkey -M menuselect 'h' vi-backward-char        # left
bindkey -M menuselect 'k' vi-up-line-or-history   # up
bindkey -M menuselect 'l' vi-forward-char         # right
bindkey -M menuselect 'j' vi-down-line-or-history # bottom

## virtualenv prompt info
function venv_prompt_info() {
    if [[ -n "$VIRTUAL_ENV" ]]; then
        if [[ -f "$VIRTUAL_ENV/__name__" ]]; then
            local name=$(cat $VIRTUAL_ENV/__name__)
        else
            local name=$(basename $VIRTUAL_ENV)
        fi
        echo "%F{magenta}[%F{blue}$name%F{magenta}]%f "
    fi
}

## Prompt customization
zstyle ':prompt:grml:left:setup' items rc path vcs venv percent
zstyle ':prompt:grml:left:items:venv' token '$(venv_prompt_info)'
zstyle ':prompt:grml:left:items:path' token '%1~ '

## vcs_info stuff: enable only git + use custom color
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git:*' formats '%F{magenta}(%F{green}%b%F{magenta})%f '

## For venv_prompt_info() to be evaluated
setopt promptsubst

## Unalias some grml stuff
unalias l lh ls
unfunction vim

alias l.='ls -d .*'
alias ll='ls -l'
alias la='ls -la'

alias grep='grep --color=auto'
alias tree='tree -CF'

alias -g '...'='../..'
alias -g '....'='../../..'
alias -g L='|less'

## Shortcuts to start development servers
alias http.server='python3 -m http.server 8080 &>/dev/null </dev/null &'
alias php.server='php -S0.0.0.0:8080 index.php &>/dev/null </dev/null &'

## Change to the root of a git repo (groot is unusable because
## there are so many groo* binaries on my PATH)
alias Groot='cd "$(git rev-parse --show-toplevel)"'

export MANWIDTH=80
export LESS="-X -F -R -x4 -i"
export EDITOR="vim"

## Configure interactive Python if available
[[ -r ~/.pythonstartup ]] && export PYTHONSTARTUP=~/.pythonstartup

## Add local binaries to PATH
for dir in ~/bin ~/.local/bin; do
    [[ -d $dir ]] && path=($dir $path)
done

## Generates random alphanumeric passwords
##
## Optional parameters:
##  1) length (default: 14)
##  2) extra characters to choose from (whitespace will be stripped)
##
## Example: genpass 14 '- + /'
genpass() {
    local extra="${2//[[:space:]]/}"
    LC_ALL=C tr -cd "[:alnum:]$extra" </dev/urandom | head -c "${1:-14}"; echo
}

## Edit files with `sudo -e`, preserving file ownership
pedit () {
    local file="$1"
    if [[ -z "$file" || ! -e "$file" ]]; then
        echo "error: no filename given or invalid file" >&2
        return 42
    fi

    # try Linux version first, then Mac:
    local oid="$(stat -c '%u' "$file" || stat -f '%u' "$file")" 2>/dev/null
    if [[ $? != 0 || -z "$oid" ]]; then
        echo "error: could not determine file ownership" >&2
        return 43
    fi

    sudo -e -u "#$oid" "$file"
}

## Load platform-specific configs
xsource ~/.zshrc.$(uname -s | tr '[:upper:]' '[:lower:]')

## Load host-specific configs (git-ignored)
xsource ~/.zshrc.host
